import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.vanniktech.maven.publish' version "0.25.2"
    id 'org.jetbrains.dokka'
}

apply from: 'quality/static-check.gradle'

android {
    namespace 'com.himanshoe.kalendar'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.13'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    lint {
        abortOnError false
        warningsAsErrors true
    }
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.S01)
    signAllPublications()
}

dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material

    //Compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.ui
    implementation libs.ui.graphics
    implementation libs.androidx.animation
    implementation libs.ui.tooling.preview
    implementation libs.material3
    debugImplementation libs.ui.tooling
    implementation libs.androidx.ui.util
    implementation libs.androidx.material.icons.extended

    //time
    implementation libs.kotlinx.datetime
    lintChecks(libs.compose.lint.checks)

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core

    coreLibraryDesugaring libs.desugar.jdk.libs
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}